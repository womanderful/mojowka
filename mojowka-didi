#!/usr/bin/env perl

=head1 NAME

mojowka-didi

=head1 DESCRIPTION

mojowka-didi is a L<mojowka|https://github.com/shoorick/mojowka> spin-off with reduced functionality—down to the feature set of L<didiwiki|http://didiwiki.wikidot.com/>.

=head1 AUTHOR

Alexander Sapozhnikov
L<http://shoorick.ru/>
L<< E<lt>shoorick@cpan.orgE<gt> >>
2000-2015

Natalia Serrano
L<< E<lt>natalia@womanderful.comE<gt> >>
2020

=head1 LICENSE

This program is free software, you can redistribute it and/or modify it under the same terms as Perl itself.

=cut

use utf8;
use open ':encoding(UTF-8)';
use Mojolicious::Lite;
use Locale::TextDomain qw/mojowka locale/;
use File::Basename 'basename';
use File::Path     'mkpath';
use Mojo::ByteStream 'b';
use Mojo::Log;
use Text::Markdown;

our $VERSION=0.05;

my $log = Mojo::Log->new;
my $DATADIR = $ENV{'MOJOWKA_DIDI_DATADIR'} || '/var/lib/mojowka';
my $md = Text::Markdown->new;
my $index_text = <<'EOF';
## Welcome to mojowka-didi

Mojowka-didi is a small and simple wiki implementation based on `mojowka` (which in turn is based on `Mojolicious::Lite`).

There is no setup needed. No database or user authentication.

It's intended for personal note-taking, "to do" lists, and any other uses you can think of. You can write articles in plain text or markdown.
EOF

# i18n
## https://github.com/mojolicious/mojo/commit/503dec92a504a68165d62ad4cc22f5c05f5e6a9a#diff-58aa583130bcb90490b462f556b92f86
app->types->type (html => 'text/html;charset=utf-8');
app->renderer->encoding ('utf-8');
app->hook (
  before_dispatch => sub {
    shift->req->default_charset ('utf-8')->url->query->charset ('utf-8');
  }
);
plugin 'textdomain' => {
    'domain'      => 'mojowka',
    'search_dirs' => [ 'locale' ],
    'available_languages' => [ 'en', 'es', 'ru' ],
    'default_language'    => 'en',
};
app->hook ('before_routes' => sub {
    my $self = shift;
    $self->set_language ($self->session ('language') || $self->detect_language);
});
app->types->type ('html' => 'text/html; charset=utf-8');
app->secrets (['mojoWka-didi']);


# Routes

under sub {
    my $self = shift;
    $self->stash ('article_title'             => '');
    $self->stash ('menu_index'                => __ 'Main page');
    $self->stash ('menu_sitemap'              => __ 'Sitemap');
    $self->stash ('menu_tags'                 => __ 'Tags');
    $self->stash ('menu_edit'                 => __ 'Edit');
    $self->stash ('menu_language'             => __ 'Language');
    $self->stash ('layout_error_deftitle'     => __ 'ERROR');
    $self->stash ('layout_default_deftitle'   => __ 'Welcome');
    $self->stash ('form_dt_title'             => __ 'Title');
    $self->stash ('form_dt_content'           => __ 'Content');
    $self->stash ('form_toolbar_bold_title'   => __ 'Bold (Ctrl+B)');
    $self->stash ('form_toolbar_bold_text'    => __ 'Bold');
    $self->stash ('form_toolbar_italic_title' => __ 'Italic (Ctrl+I)');
    $self->stash ('form_toolbar_italic_text'  => __ 'Italic');
    $self->stash ('form_toolbar_h1_title'     => __ 'Header 1');
    $self->stash ('form_toolbar_h1_text'      => __ 'Header 1');
    $self->stash ('form_toolbar_h2_title'     => __ 'Header 2');
    $self->stash ('form_toolbar_h2_text'      => __ 'Header 2');
    $self->stash ('form_toolbar_h3_title'     => __ 'Header 3');
    $self->stash ('form_toolbar_h3_text'      => __ 'Header 3');
    $self->stash ('form_toolbar_ul_title'     => __ 'Unordered list item');
    $self->stash ('form_toolbar_ul_text'      => __ 'Unordered list item');
    $self->stash ('form_toolbar_ol_title'     => __ 'Ordered list item');
    $self->stash ('form_toolbar_ol_text'      => __ 'Ordered list item');
    $self->stash ('form_md_syntax'            => __ 'Markdown syntax');
    $self->stash ('form_submit_value'         => __ 'Save');
    $self->stash ('form_preview'              => __ 'Preview');
    $self->stash ('form_delete_confirm'       => __ 'Do you really want to delete this page?');
    $self->stash ('form_delete'               => __ 'Delete page');
};

get '/set_language'           => \&set_language;
get '/set_language/:language' => \&set_language;

post '/preview' => sub {
    my $self = shift;
    my $text = $self->param ('article') || return;
    $self->render ('text' => markup ($text));
};

get '/sitemap' => sub {
    my $self = shift;

    my $now = time;
    my %mtimes = (
        '/now'      => { mtime => $now,               header => __ 'Recent',              },
        '/ago_7d'   => { mtime => $now - 86400*7,     header => __ 'Older than 7 days',   },
        '/ago_1m'   => { mtime => $now - 86400*30,    header => __ 'Older than 1 month',  },
        '/ago_3m'   => { mtime => $now - 86400*91,    header => __ 'Older than 3 months', },
        '/ago_6m'   => { mtime => $now - 86400*182,   header => __ 'Older than 6 months', },
        '/ago_1y'   => { mtime => $now - 86400*365,   header => __ 'Older than 1 year',   },
        '/ago_2y'   => { mtime => $now - 86400*730,   header => __ 'Older than 2 years',  },
        '/ago_5y'   => { mtime => $now - 86400*1826,  header => __ 'Older than 5 years',  },
    );
    foreach my $f (glob "$DATADIR/*") {
        my $bn = basename $f;
        next if 'index.md' eq $bn;
        $mtimes{$bn}{'mtime'} = (stat $f)[9];
    }

    my @buckets;
    my $header;
    foreach my $f (
        reverse sort { $mtimes{$a}{'mtime'} <=> $mtimes{$b}{'mtime'} }
        keys %mtimes
    ) {
        if ('/' eq substr $f, 0, 1) {
            $header = $mtimes{$f}{'header'};
            next;
        }
        if ($header) {
            push @buckets, {};
            $buckets[-1]{'header'} = b ($header)->decode ('UTF-8');
        }
        push @{ $buckets[-1]{'entries'} }, b ($f)->decode ('UTF-8');
        undef $header;
    }

    return $self->render (
        'layout_title' => (__ 'Sitemap'),
        'buckets'      => \@buckets,
        'header'       => (__ 'Special pages'),
        'special1'     => (__ 'Search'),
        'special2'     => (__ 'Sitemap'),
    );
};

get '/tags' => sub {
    my $self = shift;

    my %tags;

    foreach my $f (glob "$DATADIR/*") {
        my $bn = b (basename $f)->decode ('UTF-8');
        my $has_any_tags = 0;
        open my $fd, '<', $f or do { $log->warn ("open: '$f': $!"); next; };
        while (my $line = <$fd>) {
            chomp $line;
            next if $line !~ /^\s*:([a-z0-9:_-]*):\s*$/;
            for my $t (split /:/, $1) {
                $tags{$t}{$bn} = undef;
                $has_any_tags = 1;
            }
        }
        close $fd;
        $has_any_tags or $tags{':untagged'}{$bn} = undef;
    }

    return $self->render (
        'layout_title' => (__ 'Tags'),
        'tags'         => \%tags,
        'untagged'     => (__ 'Untagged'),
        'header'       => (__ 'Special pages'),
        'special1'     => (__ 'Search'),
        'special2'     => (__ 'Sitemap'),
    );
};

get '/search/:query'                   => \&search;
get '/search'                          => \&search;
any '/edit/#title'                     => \&edit_article;
any '/edit'                            => \&edit_article;

post '/rename' => sub {
    my $self = shift;
    my $src = $self->param ('src');
    my $dst = $self->param ('dst');

    if (!$src or !$dst) {
        $self->flash ('message' => __ 'Source and/or destination are empty');
        return $self->redirect_to ("/edit/$src");
    }
    $log->debug ("renaming '$src to '$dst'");
    if (-f "$DATADIR/$dst") {
        $log->debug ("dst '$dst' already exists");
        $self->flash ('message' => __ 'Destination already exists');
        return $self->redirect_to ("/edit/$src");
    }
    if (!rename "$DATADIR/$src", "$DATADIR/$dst") {
        $log->warn ("rename: '$src' to '$dst': $!");
        $self->flash ('message' => __ 'Error renaming article');
        return $self->redirect_to ("/edit/$src");
    }

    $self->flash ('message' => __ 'Article successfully renamed');
    return $self->redirect_to ("/$dst");
};

any '/delete/#title'                   => \&delete_article;
get '/#title'                          => \&get_article;
get '/'                                => \&get_article;

app->start (@ARGV ? @ARGV : 'daemon');


=head1 FUNCTIONS

=head2 markup

Convert lightweight markup into HTML.

=cut

sub markup {
    local $_ = shift || return;

    return $md->markdown ($_);
}


=head2 create_index

Create the landing page.

=cut

sub create_index {
    open my $fd, '>', "$DATADIR/index.md" or do {
        $log->warn ("open: '$DATADIR/index.md': $!");
        return;
    };
    print $fd $index_text or do {
        $log->warn ("write: '$DATADIR/index.md': $!");
        return;
    };
    close $fd;
    return 1;
}


=head2 get_article

Get article by title.

=cut

sub get_article {
    my $self  = shift;
    my $title = $self->param ('title') || '';

    if (!$title) {
        if (!-e "$DATADIR/index.md") {
            $log->debug ('index.md page does not exist');
            if (!create_index) {
                return $self->render (
                    'template'         => 'internal_server_error',
                    'title'            => (__ 'ERROR 500'),
                    'header'           => (__ 'Error'),
                    'subheader'        => (__ "Error opening or writing the document you requested."),
                    'status'           => 500,
                    'link_search_text' => (__x  'Search for &ldquo;{title}&rdquo;', 'title' => $title),
                );
            }
            $log->debug ('index page created');
        }
        return $self->redirect_to ('/index.md');
    }

    $title = basename $title;

    my $file = "$DATADIR/$title";
    $log->debug ("file is '$file'");

    return $self->render (
        'template'         => 'not_found',
        'title'            => (__ 'Not Found - ERROR 404'),
        'header'           => (__ 'Not found'),
        'subheader'        => (__ 'Document you requested was not found.'),
        'link_search_text' => (__x 'Search for &ldquo;{title}&rdquo;',  'title' => $title),
        'link_edit_text'   => (__x 'Create page &ldquo;{title}&rdquo;', 'title' => $title),
    ) unless -e $file;

    open my $fd, '<', $file or do {
        $log->warn ("open: '$file': $!");
        return $self->render (
            'template'         => 'internal_server_error',
            'title'            => (__ 'ERROR 500'),
            'header'           => (__ 'Error'),
            'subheader'        => (__ "Error opening the document you requested."),
            'status'           => 500,
            'link_search_text' => (__x  'Search for &ldquo;{title}&rdquo;', 'title' => $title),
        );
    };
    my $contents; { local $/; $contents = <$fd>; }
    close $fd;

    if ($title !~ /\.md$/) {
        ## wrap the contents between <pre></pre> markers so we get a text/plain output
        $contents = "<pre>\n$contents</pre>\n";
    }
    $self->render (
        'content'   => ($contents ? markup ($contents) : ''),
        'title'  => $title,
        'layout' => 'default',
        'show_edit' => 1,
    );
}


=head2 edit_article

Draw editing form for article found by its title and process this form.

=cut

sub edit_article {
    my $self = shift;
    my $article_title   = $self->param ('title') || '';
    my $article_content = $self->param ('article');
    $article_title = basename $article_title;

    my $file = "$DATADIR/$article_title";
    $log->debug ("file is '$file'");

    if ($self->req->method eq 'POST') {
        my @errors = ();

        unless ($article_content) {
            return $self->render (
                'errors'                    => [ __ 'Page content can not be empty' ],
                'template'                  => 'edit_article',
                'title'                     => b (__ 'Edit page')->decode ('UTF-8'),
                'article_content'           => $article_content,
                'article_title'             => $article_title,
                'show_rename' => 1,
            );
        }

        open my $fd, '>', $file or do {
            $log->warn ("open: '$file': $!");
            push (@errors, __ 'Can not save: error opening article');
            goto END;
        };

        $article_content = sprintf "%s\n", join "\n", split /\x0d\x0a/, $article_content;
        print $fd $article_content or do {
            $log->warn ("write: '$file': $!");
            push (@errors, __ 'Can not save: error writing article');
            goto END;
        };
        close $fd;

        $self->flash ('message' => __ 'Changes were saved');
        $self->redirect_to ("/$article_title");

        END:
        return $self->render (
            'errors'                    => [ @errors ],
            'template'                  => 'edit_article',
            'title'                     => b (__ 'Errors occured')->decode ('UTF-8'),
            'article_content'           => $article_content,
            'article_title'             => $article_title,
            'show_rename' => 1,
        );
    }

    my $contents = '';
    if (-e $file) {
        open my $fd, '<', $file or do {
            $log->warn ("open: '$file': $!");
            return $self->render (
                'template'         => 'internal_server_error',
                'title'            => (__ 'ERROR 500'),
                'header'           => (__ 'Error'),
                'subheader'        => (__ "Error opening the document you requested."),
                'status'           => 500,
                'link_search_text' => (__x  'Search for &ldquo;{title}&rdquo;', 'title' => $article_title),
            );
        };
        { local $/; $contents = <$fd>; }
        close $fd;
    }

    return $self->render (
        'template'                  => 'edit_article',
        'title'                     => b (__ 'Edit page')->decode ('UTF-8'),
        'article_content'           => $contents,
        'article_title'             => $article_title,
        'show_rename' => 1,
    );
}


=head2 delete_article

Delete article specified by title.

=cut

sub delete_article {
    my $self = shift;
    my $title = basename $self->param ('title') || return;

    my $file = "$DATADIR/$title";
    $log->debug ("file is '$file'");

    return $self->render (
        'template'         => 'not_found',
        'title'            => (__ 'Not Found - ERROR 404'),
        'header'           => (__ 'Not found'),
        'subheader'        => (__ 'Document you requested was not found.'),
        'link_search_text' => (__x 'Search for &ldquo;{title}&rdquo;',  'title' => $title),
        'link_edit_text'   => (__x 'Create page &ldquo;{title}&rdquo;', 'title' => $title),
    ) unless -e $file;

    if (unlink $file) {
        $log->debug ("'$file' successfully unlinked");
        $self->flash ('message' => __ 'Page was deleted');
        return $self->redirect_to ('/');
    } else {
        $log->warn ("unlink: '$file': $!");
        return $self->render (
            'template'         => 'internal_server_error',
            'title'            => (__ 'ERROR 500'),
            'header'           => (__ 'Error'),
            'subheader'        => (__ "Error deleting the document you requested."),
            'status'           => 500,
            'link_search_text' => (__x  'Search for &ldquo;{title}&rdquo;', 'title' => $title),
        );
    }
}

=head2 search

Search pages by title and content.

=cut

sub search {
    my $self = shift;

    my $title = __ 'Search';
    my $query = $self->param ('query') || '';
    my $rows  = [];

    $log->debug ("search query is '$query'");
    eval {
        'test' =~ qr/$query/;
    };
    if ($@ and 'Eval-group not allowed at runtime' eq substr $@, 0, 33) {
        $log->warn ("eval: $@");
        $self->flash ('message' => __ 'Invalid regular expression');
        return $self->redirect_to ('/search');
    }

    if ($query) {
        my @files = map { basename $_ } glob "$DATADIR/*";
        $log->debug (sprintf 'performing search on %d files', scalar @files);
        foreach my $f (@files) {
            open my $fd, '<', "$DATADIR/$f" or do {
                $log->warn ("open: '$DATADIR/$f': $!");
                next;
            };
            my $contents; { local $/; $contents = <$fd>; }
            close $fd;
            if ($f =~ qr/$query/i or $contents =~ qr/$query/i) {
                push @$rows, $f;
            }
        }

        if (1 == @$rows) {
            $log->debug ('unique match found');
            $self->flash ('message' => __ 'Exact matching found');   ## this can't currently be seen, as the template for /$title doesn't show it     TODO is this still true
            return $self->redirect_to ("/$rows->[0]");
        }

        $title = __nx
            'Found {count} page',
            'Found {count} pages',
            scalar @$rows,
            'count' => scalar @$rows;

        foreach my $row (@$rows) {
            $row = b ($row)->decode ('UTF-8');
        }
    }

    return $self->render (
        'template'         => 'search',
        'query'            => $query,
        'submit_value'     => (__ 'Find'),
        'rows'             => $rows,
        'title'            => b ($title)->decode ('UTF-8'),
    );
}


=head2 set_language

Set preferred language.
Called as set_language/I<ln> or as set_language?language=I<ln>, where I<ln> is language code.
In version 0.0.3 available 2 languages: English and Russian.

=cut

sub set_language {
    my $self = shift;
    $self->session ('language' => $self->param ('language'));
    $self->redirect_to ($self->req->headers->referrer || '/');
}

__DATA__

@@ title.html.ep
% layout 'default';
%== $content


@@ sitemap.html.ep
% layout 'default', 'title' => $layout_title;
% for my $bucket (@$buckets) {
<h4><%= $bucket->{'header'} %></h4>
<ul>
%     for my $link (@{ $bucket->{'entries'} }) {
    <li><a href="<%= url_for "/$link" %>"><%= $link %></a></li>
%     }
</ul>
% }
<h2><%= $header %></h2>
<ul>
<li><a href="<%= url_for '/search'  %>"><%= $special1 %></a></li>
<li><a href="<%= url_for '/sitemap' %>"><%= $special2 %></a></li>
</ul>


@@ tags.html.ep
% layout 'default', 'title' => $layout_title;
<ul>
% for my $tag (sort grep { ':untagged' ne $_ } keys %$tags) {
    <li><%= $tag %>:
%     for my $link (sort keys %{ $tags->{$tag} }) {
        <a href="<%= url_for "/$link" %>"><%= $link %></a>
%     }
    </li>
% }
</ul>
<h2><%= $untagged %></h2>
<ul>
%     for my $link (sort keys %{ $tags->{':untagged'} }) {
    <li><a href="<%= url_for "/$link" %>"><%= $link %></a></li>
%     }
</ul>
<h2><%= $header %></h2>
<ul>
<li><a href="<%= url_for '/search'  %>"><%= $special1 %></a></li>
<li><a href="<%= url_for '/sitemap' %>"><%= $special2 %></a></li>
</ul>


@@ search.html.ep
% layout 'default';
<form action="<%= url_for '/search' %>">
<input type="text" name="query" value="<%= $query %>">
<input type="submit" value="<%= $submit_value %>">
</form>
<ol>
% for my $row (@$rows) {
%   my $link = $row;
    <li><a href="<%= url_for "/$link" %>"><%= $row %></a></li>
% }
</ol>


@@ not_found.html.ep
% layout 'error';
<h1><%= $header %></h1><p><%= $subheader %></p>
<ul>
% my $article_title = param 'title';
<li><a href="<%= url_for "/search/$article_title" %>"><%== $link_search_text %></a></li>
<li><a href="<%= url_for "/edit/$article_title"   %>"><%== $link_edit_text   %></a></li>
</ul>


@@ internal_server_error.html.ep
% layout 'error';
% my $action = stash 'action';
<h1><%= $header %></h1><p><%= $subheader %></p>
<ul>
% my $article_title = param 'title';
<li><a href="<%= url_for "/search/$article_title" %>"><%== $link_search_text %></a></li>
</ul>


@@ edit_article.html.ep
% layout 'default';
% use Mojo::ByteStream 'b';
<%= javascript '/jquery.min.js' %>
<%= javascript begin %>
    if (typeof jQuery == 'undefined') {
      var e = document.createElement('script');
      e.src = '/js/jquery.js';
      e.type = 'text/javascript';
      document.getElementsByTagName("head")[0].appendChild(e);
    }
<% end %>
<script type="text/javascript" src="<%= url_for '/jquery-ui-1.8.11.custom.min.js' %>"></script>
<script type="text/javascript" src="<%= url_for '/jquery.caret.1.02.min.js' %>"></script>
<script type="text/javascript">
function wrap(prefix, suffix) {
    with ($('#article')) {
        var inner = caret().text;
        var start = caret().start;
        var end   = caret().end;
        val (caret().replace (prefix + inner + suffix));
        caret({ start: start+prefix.length, end:end+prefix.length });
    }
    return false;
}

$(document).ready(function() {

    // Tie onclick action to all .ajax links
    $('a#preview_link').click(function() {
        $.ajax({
            url:        '<%= url_for '/preview' %>',
            type:       'post',
            data:       { article: $('#article').val() },
            dataType:   'html',
            success:    function(data, textStatus) {
                $('div#preview_area').html(data);
                $('div#preview_area').show();
            },
            error:      function() {
                alert('Can not preview');
            }
        });
        return false;
    });
    $('#article').resizable({
        handles: 's'
    });

    $('#btn_bold').click(function(){ wrap('*', '*') });
    $('#btn_italic').click(function(){ wrap('_', '_') });
    $('#btn_h1').click(function(){ wrap('\n\nh1. ', '\n\n') });
    $('#btn_h2').click(function(){ wrap('\n\nh2. ', '\n\n') });
    $('#btn_h3').click(function(){ wrap('\n\nh3. ', '\n\n') });
    $('#btn_ul').click(function(){ wrap('\n\n* ', '\n\n') });
    $('#btn_ol').click(function(){ wrap('\n\n# ', '\n\n') });
    $('#article').keypress(function(e){
        switch (e.which) {
            case 2: wrap('*', '*'); break;
            case 9: wrap('_', '_'); break;
        }
    });
});
</script>
<style type="text/css">
.ui-resizable { position: relative}
.ui-resizable-handle { position: absolute;font-size: 0.1px;z-index: 99999; display: block;}
.ui-resizable-disabled .ui-resizable-handle, .ui-resizable-autohide .ui-resizable-handle { display: none; }
.ui-resizable-s { cursor: s-resize; height: 20px; width: 100%; bottom: 11px; left: 0;background:url(<%= url_for '/grip.gif' %>) no-repeat right top;margin:0 -4px }
#toolbar a { margin: 0 2px; padding: 0 0.3em; border: 1px outset #999; text-decoration:none; color: #000 }
button { background:url(<%= url_for '/toolbar.gif' %>) no-repeat 0 50%;width:22px;height:22px;text-indent:-9999em;border:1px outset #999;-moz-border-radius:2px;border-radius:3px}
button:hover{border-color:#69c}
#btn_italic { background-position:  -20px 50% }
#btn_h1 { background-position:  -40px 50% }
#btn_h2 { background-position:  -60px 50% }
#btn_h3 { background-position:  -80px 50% }
#btn_ul { background-position: -100px 50% }
#btn_ol { background-position: -120px 50% }
</style>

% if (my $errors = stash 'errors') {
    <ul id="errors">
%   for my $error (@$errors) {
        <li><%= b ($error)->decode ('UTF-8')->to_string %></li>
%   }
    </ul>
% }

<form method="post" action="<%= url_for "/edit/$article_title" %>">
<dl>
<dt><%= $form_dt_title %></dt>
<dd><input type="text" name="title" value="<%= $article_title %>" readonly=""></dd>
<dt><%= $form_dt_content %></dt>
<dd><span id="toolbar">
<button type="button" id="btn_bold"   title="<%= $form_toolbar_bold_title   %>"><%= $form_toolbar_bold_text   %></button>
<button type="button" id="btn_italic" title="<%= $form_toolbar_italic_title %>"><%= $form_toolbar_italic_text %></button>
<button type="button" id="btn_h1"     title="<%= $form_toolbar_h1_title     %>"><%= $form_toolbar_h1_text     %></button>
<button type="button" id="btn_h2"     title="<%= $form_toolbar_h2_title     %>"><%= $form_toolbar_h2_text     %></button>
<button type="button" id="btn_h3"     title="<%= $form_toolbar_h3_title     %>"><%= $form_toolbar_h3_text     %></button>
<button type="button" id="btn_ul"     title="<%= $form_toolbar_ul_title     %>"><%= $form_toolbar_ul_text     %></button>
<button type="button" id="btn_ol"     title="<%= $form_toolbar_ol_title     %>"><%= $form_toolbar_ol_text     %></button>
</span>
<br><textarea name="article" id="article"><%= $article_content %></textarea>
<br><a href="https://daringfireball.net/projects/markdown/syntax"><%= $form_md_syntax %></a>
</dd>
</dl>
<input type="submit" value="<%= $form_submit_value %>">
<a href="#" id="preview_link"><%= $form_preview %></a>
    <a href="<%= url_for "/delete/$article_title" %>" id="delete_link"
    onclick="return confirm('<%= $form_delete_confirm %>')"><%= $form_delete %></a>
</form>
<div id="preview_area"></div>


@@ layouts/error.html.ep
<!DOCTYPE html>
<html>
    <head>
        <title><%= $title || $layout_error_deftitle %></title>
        <style type="text/css">body{background:#fec;color:#000}h1{color:#900}</style>
        <link rel="stylesheet" type="text/css" href="/style.css">
    </head>
    <body>
%= include 'menu';
        <%= content %>
    </body>
</html>


@@ layouts/default.html.ep
% use Mojo::ByteStream 'b';
<!DOCTYPE html>
<html>
    <head><title><%= $title || $layout_default_deftitle %></title>
    <link rel="shortcut icon" href="<%= url_for '/favicon.ico' %>">
    <link rel="stylesheet" type="text/css" href="/style.css">
    </head>
    <body>
%= include 'menu';
% if (my $message = flash 'message') {
        <div id="message"><%= b ($message)->decode ('UTF-8')->to_string %></div>
% }

    <h1><%= $title %></h1>
    <%= content %>
    </body>
</html>


@@ menu.html.ep
<ul id="menu">
    <li><a href="<%= url_for '/index.md' %>"><%= $menu_index   %></a></li>
    <li><a href="<%= url_for '/sitemap'  %>"><%= $menu_sitemap %></a></li>
    <li><a href="<%= url_for '/tags'     %>"><%= $menu_tags    %></a></li>
    <% if (stash 'show_edit') { %><li><a href="<%= url_for "/edit/$title" %>"><%= $menu_edit %></a></li><% } %>
    <li><form action="<%= url_for '/set_language' %>" onchange="this.submit()"><select name="language"><option value=""><%= $menu_language %></option><option value="en">English</option><option value="ru">Русский</option><option value="es">Español</option></select></form></li>
    <% if (stash 'show_rename') { %><li><form method="post" action="<%= url_for '/rename' %>"><input type="hidden" name="src" value="<%= $article_title %>"><input id="rename" type="text" name="dst"></form></li><% } %>
    <li><form action="<%= url_for '/search' %>"><input id="search" type="text" name="query"></form></li>
    <li><form action="<%= url_for '/edit'   %>"><input id="edit"   type="text" name="title"></form></li>
</ul>
